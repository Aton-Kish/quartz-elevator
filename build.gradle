plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id "maven-publish"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	// Cloth Config
	maven {
		url "https://maven.shedaniel.me/"
	}

	// ModMenu
	maven {
		url "https://maven.terraformersmc.com/releases/"
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"quartzelv" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Uncomment the following line to enable the deprecated Fabric API modules.
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

	// Auto Config:
	// see https://shedaniel.gitbook.io/cloth-config/
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

curseforge {
	apiKey = project.findProperty("curseforge_token") ?: "Missing token"

	project {
		id = project.curseforge_id

		mainArtifact(remapJar) {
			displayName = "Quartz Elevator v${project.version}"
		}
		releaseType = "release"
		changelogType = "markdown"
		changelog = "[v${project.version}](https://github.com/Aton-Kish/quartz-elevator/releases/tag/v" + java.net.URLEncoder.encode(project.version, "UTF-8") + ")"

		addGameVersion "Fabric"
		addGameVersion "Java 17"
		addGameVersion "1.20"
		addGameVersion "1.20.1"

		relations {
			requiredDependency "fabric-api"
			embeddedLibrary "cloth-config"
			optionalDependency "modmenu"
		}
	}
}

modrinth {
	token = project.findProperty("modrinth_token") ?: "Missing token"

	projectId = project.modrinth_id

	uploadFile = remapJar
	versionNumber = project.version
	versionName = "Quartz Elevator v${project.version}"
	versionType = "release"
	changelog = "[v${project.version}](https://github.com/Aton-Kish/quartz-elevator/releases/tag/v" + java.net.URLEncoder.encode(project.version, "UTF-8") + ")"

	gameVersions = ["1.20", "1.20.1"]
	loaders = ["fabric"]

	dependencies {
		required.project "fabric-api"
		embedded.project "cloth-config"
		optional.project "modmenu"
	}
}
